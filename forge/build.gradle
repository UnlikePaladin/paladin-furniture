plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name

        mixinConfig "pfm-common.mixins.json"
        
    }

}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

repositories {
    maven { url 'https://maven.blamejared.com' }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
}


dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"

    modCompileOnly("vazkii.patchouli:Patchouli:${rootProject.patchouli_version_forge}")

    modApi("me.shedaniel.cloth:cloth-config-forge:${rootProject.cloth_config_version}")

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }
}

dependencies {
    modCompileOnlyApi("me.shedaniel:RoughlyEnoughItems-api-forge:${rootProject.rei_version}")
    modCompileOnlyApi "me.shedaniel:RoughlyEnoughItems-default-plugin-forge:${rootProject.rei_version}"
    modCompileOnlyApi("mezz.jei:jei-1.19.3-common-api:${rootProject.jei_version}")
    modCompileOnlyApi("mezz.jei:jei-1.19.3-forge-api:${rootProject.jei_version}")
}

if (rootProject.jei_compatibility == "true"){
    dependencies {
        modRuntimeOnly("mezz.jei:jei-1.19.3-forge:${rootProject.jei_version}")
    }
}

if (rootProject.rei_compatibility == "true"){
    dependencies {
        modImplementation("me.shedaniel:RoughlyEnoughItems-forge:${rootProject.rei_version}")
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
    duplicatesStrategy = 'include'
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null
    archiveClassifier = "forge-mc${rootProject.minecraft_version}"
}

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
    duplicatesStrategy = 'include'
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
