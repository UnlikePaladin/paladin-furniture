plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    runs {
        datagen {
            server()

            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"

            runDir "build/datagen"
        }
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

repositories {
    maven { url "https://maven.shedaniel.me" }
    maven { url 'https://maven.blamejared.com' }
    maven {
        name 'Gegy'
        url 'https://maven.gegy.dev'
    }
    maven { url "https://maven.terraformersmc.com/releases/" }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven { url 'https://jitpack.io' }
    maven {
        url = uri("https://storage.googleapis.com/devan-maven/")
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        url "https://maven.twelveiterations.com/repository/maven-public/"

        content {
            includeGroup "net.blay09.mods"
        }
    }
}

if (rootProject.sandwichable_compatibility == "true") {
    dependencies {
        modImplementation("maven.modrinth:sandwichable:${rootProject.sandwichable_version}"){
            exclude(group: "net.fabricmc.fabric-api")
        }
        modImplementation("dev.lambdaurora:spruceui:${rootProject.spruceui_version}"){
            exclude(group: "net.fabricmc.fabric-api")
        }
    }
}
else {
    dependencies {
        modCompileOnly("maven.modrinth:sandwichable:${rootProject.sandwichable_version}"){
            exclude(group: "net.fabricmc.fabric-api")
        }
    }
}

if (rootProject.rei_compatibility == "true"){
    dependencies {
        modRuntimeOnly("me.shedaniel:RoughlyEnoughItems-fabric:${rootProject.rei_version}"){
            transitive(false)
        }
        modRuntimeOnly("me.shedaniel.cloth:cloth-config-fabric:${rootProject.cloth_config_version}") {
            exclude(group: "net.fabricmc.fabric-api")
        }
    }
}
if (rootProject.emi_compatibility == "true"){
    dependencies {
        modRuntimeOnly("dev.emi:emi-fabric:${emi_version}"){
            exclude(group: "net.fabricmc.fabric-api")
        }
    }
}

if (rootProject.patchouli_compatibility == "true") {
    dependencies {
        modImplementation("vazkii.patchouli:Patchouli:${rootProject.patchouli_version_fabric}"){
            exclude(group: "net.fabricmc.fabric-api")
        }
    }
}
else {
    dependencies {
        modCompileOnly("vazkii.patchouli:Patchouli:${rootProject.patchouli_version_fabric}"){
            exclude(group: "net.fabricmc.fabric-api")
        }
    }
}

if (rootProject.immersive_portals_compatibility_fabric == "true"){
    dependencies {
        modImplementation ("com.github.iPortalTeam.ImmersivePortalsMod:imm_ptl_core:${rootProject.immersive_portals_version_fabric}"){
            exclude(group: "net.fabricmc.fabric-api")
            transitive(false)
        }
        modImplementation("com.github.iPortalTeam.ImmersivePortalsMod:q_misc_util:${rootProject.immersive_portals_version_fabric}"){
            exclude(group: "net.fabricmc.fabric-api")
            transitive(false)
        }
    }
}

if (rootProject.lazy_dfu_enabled == "true"){
    dependencies {
        modRuntimeOnly ("maven.modrinth:lazydfu:${rootProject.lazy_dfu_version_fabric}")
    }
}
if (rootProject.jei_compatibility == "true") {
    dependencies {
        modRuntimeOnly("mezz.jei:jei-${rootProject.minecraft_version}-fabric:${rootProject.jei_version}")
    }
}


dependencies {
    // To change the versions see the gradle.properties file

    // Fabric API. This is technically optional, but you probably want it anyway.

    modImplementation("com.terraformersmc:modmenu:${rootProject.modmenu_version}") {
        exclude(group: "net.fabricmc.fabric-api")
        transitive(false)
    }

   // modRuntimeOnly("com.terraformersmc:terrestria:${rootProject.terrestria}")
    modCompileOnly("me.shedaniel:RoughlyEnoughItems-api-fabric:${rootProject.rei_version}") {
        transitive(false)
    }
    modCompileOnly("maven.modrinth:iris:${rootProject.iris_version}")

    modCompileOnly("curse.maven:farmers-delight-fabric-482834:${rootProject.farmers_delight_version_fabric}")
    modCompileOnlyApi("mezz.jei:jei-${rootProject.minecraft_version}-fabric:${rootProject.jei_version}") {
        exclude group: "mezz.jei"
    }


    modCompileOnly "curse.maven:balm-fabric-500525:${rootProject.balm_version_fabric}"
    modCompileOnly "curse.maven:cooking-for-blockheads-fabric-634546:${rootProject.cookingforblockheads_version_fabric}"

    modCompileOnly("com.github.iPortalTeam:ImmersivePortalsMod:${rootProject.immersive_portals_version_fabric}"){
        exclude(group: "net.fabricmc.fabric-api")
        transitive(false)
    }
    modCompileOnly("com.github.iPortalTeam:DimLib:${rootProject.dimlib_version_fabric}"){
        exclude(group: "net.fabricmc.fabric-api")
        transitive(false)
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    modApi "net.fabricmc.fabric-api:fabric-api-deprecated:${project.fabric_api_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

sourceSets {
    main {
        resources {
            srcDirs += [
                    "src/main/generated"
            ]
        }
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    injectAccessWidener = true
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = "fabric-mc${rootProject.minecraft_version}"
}

jar {
    archiveClassifier = "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

configurations.all {
    resolutionStrategy {
        force("net.fabricmc:fabric-loader:$rootProject.fabric_loader_version")
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
